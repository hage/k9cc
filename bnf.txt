program         = funcdef*
funcdef         = ident "(" (ident ("," ident)*)? ")" "{" stmt* "}"
stmt            = expr ";"
                | block
                | "if" "(" expr ")" stmt ("else" stmt)?
                | "while" "(" expr ")" stmt
                | "for" "(" expr? ";" expr? ";" expr? ")" stmt
                | "return" expr ";"
                | "int" ident ";"
block           = "{" stmt* "}"
expr            = assign
assign          = equality ("=" assign)?
equality        = relational ("==" relational | "!=" relational)*
relational      = add ("<" add | "<=" add | ">" add | ">=" add)*
add             = mul ("+" mul | "-" mul)*
mul             = unary ("*" unary | "/" unary)*
unary           = ("+" | "-")? primary
                | "*" unary
                | "&" unary
primary         = num
                | ident ("(" (expr ("," expr)*)? ")")?
                | "(" expr ")"
